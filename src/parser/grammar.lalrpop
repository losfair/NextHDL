use crate::ast;
use crate::parser::error::LocalParseError;
use lalrpop_util::ParseError;
use crate::parser::grammar_helper as helper;
use crate::parser::state::State;
use std::sync::Arc;
use crate::util::{ArcSliceExt, mk_arc_slice};
use std::collections::BTreeMap;

grammar(state: &mut State);

extern {
  type Error = LocalParseError;
}

pub ModuleDef: ast::ModuleDef = {
  <c:Comment*> <items:ModuleItem*> => ast::ModuleDef { items: mk_arc_slice(items.into_iter()) },
};

ModuleItem: ast::ModuleItem = {
  <x:FnDef> => ast::ModuleItem::Fn(x),
  <x:StructDef> => ast::ModuleItem::Struct(x),
  Token<"extern"> Token<"signal"> <x:GenericDef> Token<";"> => ast::ModuleItem::ExternSignal(x),
  Token<"import"> <path:StringLit> Token<"as"> <as_name:Identifier> Token<";"> => ast::ModuleItem::Import(ast::ImportItem { path, as_name }),
};

StructDef: ast::StructDef = {
  Token<"struct"> <name:Identifier> <tyargs:TyArgList?> Token<"{"> <fields:(<StructFieldDef> ",")*> <last_field:StructFieldDef?> Token<"}"> => {
    let fields: BTreeMap<Arc<str>, ast::Expr> =
      fields.into_iter().chain(std::iter::once(last_field).filter_map(|x| x))
        .map(|x| (x.0.0, x.1))
        .collect();
    ast::StructDef { name, fields: Arc::new(fields), tyargs: tyargs.unwrap_or_else(|| Arc::new([])) }
  },
};

StructFieldDef: (ast::Identifier, ast::Expr) = {
  <name:Identifier> Token<":"> <ty:Expr> => (name, ty),
};

FnDef: ast::FnDef = {
  Token<"fn"> <name:Identifier> <meta:FnMeta> <specializations:MaybeFnSpecializations> => ast::FnDef { name, meta, specializations }
};

GenericDef: ast::GenericDef = {
  <name:Identifier> <ty:(Token<":"> <Expr>)?> <init_value:(Token<"="> <Expr>)?> => ast::GenericDef { name, ty, init_value },
};

MaybeFnSpecializations: Arc<[ast::FnSpecialization]> = {
  <w:FnSpecializationWhere*> <d:FnSpecializationDefault> => {
    mk_arc_slice(w.into_iter().chain(std::iter::once(d)))
  },
  <body:Body> => mk_arc_slice(std::iter::once(ast::FnSpecialization { where_expr: None, body })),
};

FnSpecializationWhere: ast::FnSpecialization = {
  Token<"where"> <e:Expr> <body:Body> => ast::FnSpecialization { where_expr: Some(e), body },
};

FnSpecializationDefault: ast::FnSpecialization = {
  Token<"default"> <body:Body> => ast::FnSpecialization { where_expr: None, body },
};

Body: ast::Body = {
  <loc_start:@L> Token<"{"> <body:PlainBody> Token<"}"> <loc_end:@R> => ast::Body { body, loc_start, loc_end },
};

FnMeta: ast::FnMeta = {
  <tyargs:TyArgList?> <args:FnArgList> <ret:FnRet?> => ast::FnMeta {
    tyargs: tyargs.unwrap_or_else(|| Arc::new([])),
    args,
    ret,
  },
};

FnRet: Arc<ast::Expr> = {
  Token<"->"> <ty:Expr> => Arc::new(ty),
};

TyArgList: Arc<[ast::TyArg]> = {
  Token<"<"> <list:Comma<TyArg>> Token<">"> => list,
};

TyArg: ast::TyArg = {
  <name:Identifier> Token<":"> <kind:Expr> <default_value:(Token<"="> <Expr>)?> => {
    ast::TyArg { name, kind: Some(kind), default_value }
  },
};

FnArgList: Arc<[ast::FnArg]> = {
  Token<"("> <list:Comma<FnArg>> Token<")"> => list,
};

FnArg: ast::FnArg = {
  <name:Identifier> Token<":"> <ty:Expr> => ast::FnArg { name, ty },
};

TypeAssignList: Arc<[ast::TypeAssign]> = {
  Token<"<"> <list:Comma<TypeAssign>> Token<">"> => list,
};

TypeAssign: ast::TypeAssign = {
  <e:Expr> => ast::TypeAssign { ty: None, e },
  <ty:Identifier> Token<"="> <e:Expr> => ast::TypeAssign { ty: Some(ty), e },
};

ActualArgList<I>: Arc<[ast::Expr]> = {
  Token<"("> <list:Comma<I>> Token<")"> => list,
};

Block: ast::Block = {
  Token<"{"> <args:FnArgList> Token<"in"> <body:PlainBody> Token<"}"> => ast::Block { args, body },
  Token<"{"> <body:PlainBody> Token<"}"> => ast::Block { args: mk_arc_slice(std::iter::empty()), body },
};

PlainBody: Arc<[ast::Stmt]> = {
  <v:Stmt*> => mk_arc_slice(v.into_iter()),
};

Stmt: ast::Stmt = {
  <v:StmtV> => ast::Stmt { v },
};

StmtV: ast::StmtV = {
  Token<"let"> <def:GenericDef> Token<";"> => ast::StmtV::Let { def },
  Token<"signal"> <def:GenericDef> Token<";"> => ast::StmtV::Signal { def },
  Token<"if"> <e:Expr> <if_body:Body> => ast::StmtV::IfElse { condition: e, if_body, else_body: None },
  Token<"if"> <e:Expr> <if_body:Body> Token<"else"> <else_body:Body> => ast::StmtV::IfElse {
    condition: e,
    if_body,
    else_body: Some(else_body),
  },
  <left:Identifier> Token<"="> <right:Expr> Token<";"> => ast::StmtV::Assign { left, right },
  <e:Expr> Token<";"> => ast::StmtV::Expr { e },
  <mut e:Expr> <block_start:@L> <b:Block> <block_end:@R> Token<";">? => {
    let b = ast::Expr { v: ast::ExprV::Block(Arc::new(b)), loc_start: block_start, loc_end: block_end };
    if let ast::ExprV::Call { ref mut args, .. } = e.v {
      *args = mk_arc_slice(args.take_all().chain(std::iter::once(b)));
      e.loc_end = block_end;
      ast::StmtV::Expr { e }
    } else {
      let loc_start = e.loc_start;
      ast::StmtV::Expr {
        e: ast::Expr {
          v: ast::ExprV::Call {
            base: Arc::new(e),
            args: mk_arc_slice(std::iter::once(b)),
          },
          loc_start,
          loc_end: block_end,
        },
      }
    }
  }
}

Expr_Lx<V>: ast::Expr = {
  <loc_start:@L> <v:V> <loc_end:@R> => ast::Expr { v, loc_start, loc_end }
};

Expr_L0: ast::Expr = Expr_Lx<ExprV_L0>;

ExprV_L0: ast::ExprV = {
  <l:Literal> => ast::ExprV::Lit(l),
  <id:Identifier> => ast::ExprV::Ident(id),
  <base:Expr_L0> Token<"."> <id:Identifier> => ast::ExprV::Dot { base: Arc::new(base), id },
  <base:Expr_L0> <tyassigns:TypeAssignList> => ast::ExprV::Specialize { base: Arc::new(base), tyassigns },
  <base:Expr_L0> <args:ActualArgList<Expr>> => {
    ast::ExprV::Call { base: Arc::new(base), args }
  },
  Token<"("> <inner:ExprV> Token<")"> => inner,
  <b:Block> => ast::ExprV::Block(Arc::new(b)),
};

Expr_L1: ast::Expr = Expr_Lx<ExprV_L1>;

ExprV_L1: ast::ExprV = {
  ExprV_L0,
  Token<"<"> <dsttype:Expr> Token<">"> <base:Expr_L0> => helper::gen_binop_call(state, "cast", base, dsttype),
};

Expr_L2: ast::Expr = Expr_Lx<ExprV_L2>;

ExprV_L2: ast::ExprV = {
  ExprV_L1,
  <left:Expr_L2> Token<"*"> <right:Expr_L1> => helper::gen_binop_call(state, "mul", left, right),
  <left:Expr_L2> Token<"/"> <right:Expr_L1> => helper::gen_binop_call(state, "div", left, right),
};

Expr_L3: ast::Expr = Expr_Lx<ExprV_L3>;

ExprV_L3: ast::ExprV = {
  ExprV_L2,
  <left:Expr_L3> Token<"+"> <right:Expr_L2> => helper::gen_binop_call(state, "add", left, right),
  <left:Expr_L3> Token<"-"> <right:Expr_L2> => helper::gen_binop_call(state, "sub", left, right),
  <left:Expr_L3> Token<"&"> <right:Expr_L2> => helper::gen_binop_call(state, "bitand", left, right),
  <left:Expr_L3> Token<"|"> <right:Expr_L2> => helper::gen_binop_call(state, "bitor", left, right),
};

Expr_L4: ast::Expr = Expr_Lx<ExprV_L4>;

ExprV_L4: ast::ExprV = {
  ExprV_L3,
  <left:Expr_L4> Token<"=="> <right:Expr_L3> => helper::gen_binop_call(state, "eq", left, right),
  <left:Expr_L4> Token<"!="> <right:Expr_L3> => helper::gen_binop_call(state, "ne", left, right),
};

Expr_L5: ast::Expr = Expr_Lx<ExprV_L5>;

ExprV_L5: ast::ExprV = {
  ExprV_L4,
  <left:Expr_L5> Token<"&&"> <right:Expr_L4> => helper::gen_binop_call(state, "logicand", left, right),
  <left:Expr_L5> Token<"||"> <right:Expr_L4> => helper::gen_binop_call(state, "logicor", left, right),
};

Expr: ast::Expr = Expr_Lx<ExprV>;

ExprV: ast::ExprV = {
  ExprV_L5,
  Token<"fn"> <meta:FnMeta> => ast::ExprV::Fn(meta),
};

Literal: ast::Literal = {
  <s:Token<r"[0-9]+">> =>? s.parse().map(|x| ast::Literal { v: ast::LiteralV::Uint(x) }).map_err(|_| ParseError::User {
    error: LocalParseError::InvalidLiteral,
  }),
  <s:Token<r"0x[0-9a-fA-F]+">> =>? helper::parse_radix_prefixed_str(s, "0x", 16),
  <s:Token<r"0o[0-9a-fA-F]+">> =>? helper::parse_radix_prefixed_str(s, "0o", 8),
  <s:Token<r"0b[0-9a-fA-F]+">> =>? helper::parse_radix_prefixed_str(s, "0b", 2),
  <s:StringLit> => ast::Literal { v: ast::LiteralV::String(s) },
};

StringLit: Arc<str> = {
  <s:Token<r#""(\\.|.)*""#>> =>? serde_json::from_str::<String>(s)
    .map(|x| state.get_string(&x))
    .map_err(|_| ParseError::User {
      error: LocalParseError::InvalidLiteral,
    }),
};

Identifier: ast::Identifier = {
  <s:Token<r"[a-zA-Z_][0-9a-zA-Z_]*">> => ast::Identifier(state.get_string(s)),
};

Comma<T>: Arc<[T]> = { 
  <v:(<T> Token<",">)*> <e:T?> => match e {
    None => mk_arc_slice(v.into_iter()),
    Some(e) => mk_arc_slice(v.into_iter().chain(std::iter::once(e))),
  }
};

Token<I>: I = {
  <s:I> <c:Comment*> => s,
};

Comment: () = {
  r"//[^\n\r]*[\n\r]*" => { },
  r"/\*([^\*]*\*+[^\*/])*([^\*]*\*+|[^\*])*\*/" => { },
};
